apply plugin: 'java'
apply plugin: 'maven-publish'
apply from: 'gradleScripts/util.gradle'

sourceCompatibility = 1.7
version = '1.1'

setupDefaultDirectoryStructure()

setupDefaultRepositories()

setupProjectSpecificDependencies()

task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

jar {
   manifest {
      attributes(
      "Created-By": "IHMC Gradle Build Script",
      "Implementation-Title": project.name,
      "Implementation-Version": project.version,
      "Implementation-Vendor": "IHMC",
      
      "Bundle-Name": project.name,
      "Bundle-Version": project.version,
      "Bundle-License": "http://www.apache.org/licenses/LICENSE-2.0.txt",
      "Bundle-Vendor": "IHMC")
   }
}

publishing {
	publications {
		mavenJava(MavenPublication) {

			groupId 'us.ihmc'
			artifactId 'IHMCRealtime'
			version "$version"
			from components.java
			
			pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'IHMCRealtime'
                    description 'IHMCRealtime provides a JNI-backed threading library for attaching real-time POSIX threads to a running JVM process, allowing for deterministic computation of tasks. A JNI-backed CPU Affinity library and pure Java data structures for lockless inter-thread communication'
                    url 'https://bitbucket.org/ihmcrobotics/ihmcrealtime/'
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'jsmith'
                            name 'Jesper Smith'
                            email 'jsmith@ihmc.us'
                        }
                        developer {
                        	id 'dstephen'
                        	name 'Douglas Stephen'
                        	email 'dstephen@ihmc.us'
                        }
                        developer {
                        	id 'alesman'
                        	name 'Alex Lesman'
                        }
                    }
                }

        	}
			
			artifact sourceJar {
				classifier "sources"
			}
		}

	}

	repositories {
		maven {
			credentials {
				username project.hasProperty('nexus_username')?"${nexus_username}":"invalid"
				password project.hasProperty('nexus_password')?"${nexus_password}":"invalid"
			}
			url "https://bengal.ihmc.us/nexus/content/repositories/releases"
		}
	}
}